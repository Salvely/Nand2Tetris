// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/2/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:
    // Replace this comment with your code.
    
    Mux16(a=x, b[0..15]=false, sel=zx, out=zerox);
    Not16(in=zerox, out=notzerox);
    Mux16(a=zerox, b=notzerox, sel=nx, out=outx);

    Mux16(a=y, b[0..15]=false, sel=zy, out=zeroy);
    Not16(in=zeroy, out=notzeroy);
    Mux16(a=zeroy, b=notzeroy, sel=ny, out=outy);

    Add16(a=outx, b=outy, out=addxy);
    And16(a=outx, b=outy, out=andxy);
    Mux16(a=andxy, b=addxy, sel=f, out=outxy);

    Not16(in=outxy, out=notoutxy);
    Mux16(a=outxy, b=notoutxy, sel=no, out[0..7]=outLSB,out[8..14]=outMSB,out[15]=outSign);

    Mux16(a[0..7]=outLSB, a[8..14]=outMSB, a[15]=outSign, b=true, sel=false, out=out);

    // use or16way to judge if out == 0
    Or8Way(in=outLSB, out=zeroLSB);
    Or8Way(in[0..6]=outMSB,in[7]=outSign, out=zeroMSB);
    Or(a=zeroLSB, b=zeroMSB, out=zeroOut);
    Mux(a=true, b=false, sel=zeroOut, out=zr);

    Mux(a=false, b=true, sel=outSign, out=ng);
}